apply plugin: 'maven-publish'
apply from: "../common/constants.gradle"
apply from: "../common/repos.gradle"

archivesBaseName = 'firebase-ui'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

/**
 * Prepare artifacts for this an all sub-projects.
 */
task prepareArtifacts(dependsOn: [
        javadocJar,
        sourcesJar,
        assembleRelease,
        ':database:prepareArtifacts',
        ':storage:prepareArtifacts']) {

}

/**
 * Publish all artifacts to the maven local repository.
 */
task publishAllToMavenLocal(dependsOn: [
        'publishMonolithLibraryPublicationToMavenLocal',
        ':database:publishDatabaseLibraryPublicationToMavenLocal',
        ':storage:publishStorageLibraryPublicationToMavenLocal']) {

}

/**
 * Publish all artifacts to the customLocal repository.
 */
task publishAllToCustomLocal(dependsOn: [
        'publishMonolithLibraryPublicationToCustomLocalRepository',
        ':database:publishDatabaseLibraryPublicationToCustomLocalRepository',
        ':storage:publishStorageLibraryPublicationToCustomLocalRepository']) {

}

artifacts {
    archives javadocJar
    archives sourcesJar
}


publishing {

    publications {
        monolithLibrary(MavenPublication) {
            groupId project.ext.group
            artifactId archivesBaseName
            version project.ext.version

            artifact "$buildDir/outputs/aar/$archivesBaseName-release.aar"
            artifact javadocJar
            artifact sourcesJar

            // Monolith is just a POM that depends on the others
            pom.withXml {
                def dependenciesNode = asNode().getAt("dependencies")[0]
                if (dependenciesNode == null) {
                    dependenciesNode = asNode().appendNode("dependencies");
                }

                def deps = [
                        [project.ext.group, 'firebase-ui-database', project.ext.version],
                        [project.ext.group, 'firebase-ui-storage', project.ext.version],
                ]

                // Add all that are 'compile'
               deps.each { dep ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep[0])
                    dependencyNode.appendNode('artifactId', dep[1])
                    dependencyNode.appendNode('version', dep[2])
                }
            }
        }
    }

}
